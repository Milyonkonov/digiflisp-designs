Inport	EQU	$FC
Utport	EQU	$FB
DelayC1	EQU	$5		;Dessa konstanter styr hur lång fördröjning Delay1s orsakar.
DelayC2	EQU	$2		;Ändra dem eventuellt i labbet så att fördröjnigen blir rimligt lång.

	ORG	$FF
	FCB	$20

	ORG	$20
	LDSP	#$20

Main:
	JSR	ReadAscii
	SUBA	#$41
	LSLA			; A <- 2*A
	PSHA

	LDX	#MorseCode
	LDA	A,X
	STA	Count
	
	PULA
	INCA
	LDA	A,X
	
	
BlinkLoop:	
	JSR	BlinkLight
	LSLA
	DEC	Count
	BNE	BlinkLoop
	BRA	Main


ReadAscii:			;'A' -> LDA #0 | 'B' -> LDA #1 | 'C' -> LDA #2
WaitFor0:
	LDA	Inport
	ANDA	#%00000001
	BNE	WaitFor0
WaitFor1:
	LDA	Inport
	ANDA	#%00000001
	BEQ	WaitFor1

	LDA	Inport
	LSRA
	RTS


	
BlinkLight:
	PSHA			;A innehåller bitmasken för morsekoden
	LDA	#$FF
	STA	Utport	
	PULA

	PSHA
	LDY	#1
	ANDA	#%10000000	;Om mest signifikanta biten i A är =1 sker en långsam blinkning
	BEQ	DelayLoop
	LDY	#4
DelayLoop:
	JSR	Delay1s
	LEAY	-1,Y
	CMPY	#0
	BNE	DelayLoop

	LDA	#0
	STA	Utport
	JSR	Delay1s
	PULA
	RTS	
	
; Orsakar en fördröjning på ungefär 1s i simulatorn (vid snabb exekveringshastighet)
; Ändrar inga register
Delay1s:
	PSHA
	PSHX
	PSHC
	LDX	#DelayC2
Delay1s_loop2:
	LDA	#DelayC1
Delay1s_loop:
	DECA
	BNE	Delay1s_loop
	LEAX	-1,X
	CMPX	#0
	BNE	Delay1s_loop2
	PULC
	PULX
	PULA
	RTS

; Data - Variabler
Count:
	RMB	1

; Data - Konstanter
MorseCode:
	FCB	2,%01000000 ;'A'
	FCB	4,%10000000 ;'B'
	FCB	4,%10100000 ;'C'
	FCB	3,%10000000 ;'D'
	FCB	1,%00000000 ;'E'
	FCB	4,%00100000 ;'F'
	FCB	3,%11000000 ;'G'
	FCB	4,%00000000 ;'H'
	FCB	2,%00000000 ;'I'
	FCB	4,%01110000 ;'J'
	FCB	3,%10100000 ;'K'
	FCB	4,%01000000 ;'L'
	FCB	2,%11000000 ;'M'
	FCB	2,%10000000 ;'N'
	FCB	3,%11100000 ;'O'
	FCB	4,%01100000 ;'P'
	FCB	4,%11010000 ;'Q'
	FCB	3,%01000000 ;'R'
	FCB	3,%00000000 ;'S'
	FCB	1,%10000000 ;'T'
	FCB	3,%00100000 ;'U'
	FCB	4,%00010000 ;'V'
	FCB	3,%01100000 ;'W'
	FCB	4,%10010000 ;'X'
	FCB	4,%10110000 ;'Y'
	FCB	4,%11000000 ;'Z'
	
	
	
	
	
	